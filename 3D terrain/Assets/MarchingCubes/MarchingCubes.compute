#pragma kernel March
#include "MarchingTables.compute"

struct Triangle {
    float3 vertexC;
    float3 vertexB;
    float3 vertexA;
};

AppendStructuredBuffer<Triangle> triangles;
RWStructuredBuffer<float4> points;
AppendStructuredBuffer<int3> debuging;

//these variables cannot be static or else the values don't get populated
int numPointsPerAxis;
float surface;

float3 interpolateVerts(float4 v1, float4 v2) {
    if((v2.w - v1.w) == 0 || (surface - v1.w) == 0) return v1.xyz + surface * (v2.xyz-v1.xyz);
    float t = (surface - v1.w) / (v2.w - v1.w);
    return v1.xyz + t * (v2.xyz-v1.xyz);
}

int indexFromCoord(int x, int y, int z) {
    return z * numPointsPerAxis * numPointsPerAxis + y * numPointsPerAxis + x;
}

[numthreads(8,8,8)]
void March (int3 id : SV_DispatchThreadID)
{   
    //I THINK THIS IS WHAT WAS BREAKING IT, COULD PROCCESS DATA AFTER I REMOVED IT
    // Stop one point before the end because voxel includes neighbouring points
    if (id.x >= numPointsPerAxis-1 || id.y >= numPointsPerAxis-1 || id.z >= numPointsPerAxis-1) {
        //debuging.Append(id);
        return;
    }

    //debuging.Append(id);

    // 8 corners of the current cube
    float4 cubeCorners[8] = {
        points[indexFromCoord(id.x, id.y, id.z)],
        points[indexFromCoord(id.x + 1, id.y, id.z)],
        points[indexFromCoord(id.x + 1, id.y, id.z + 1)],
        points[indexFromCoord(id.x, id.y, id.z + 1)],
        points[indexFromCoord(id.x, id.y + 1, id.z)],
        points[indexFromCoord(id.x + 1, id.y + 1, id.z)],
        points[indexFromCoord(id.x + 1, id.y + 1, id.z + 1)],
        points[indexFromCoord(id.x, id.y + 1, id.z + 1)]
    };

    // Calculate unique index for each cube configuration.
    // There are 256 possible values
    // A value of 0 means cube is entirely inside surface; 255 entirely outside.
    // The value is used to look up the edge table, which indicates which edges of the cube are cut by the isosurface.
    int cubeIndex = 0;
    if (cubeCorners[0].w < surface) cubeIndex |= 1;
    if (cubeCorners[1].w < surface) cubeIndex |= 2;
    if (cubeCorners[2].w < surface) cubeIndex |= 4;
    if (cubeCorners[3].w < surface) cubeIndex |= 8;
    if (cubeCorners[4].w < surface) cubeIndex |= 16;
    if (cubeCorners[5].w < surface) cubeIndex |= 32;
    if (cubeCorners[6].w < surface) cubeIndex |= 64;
    if (cubeCorners[7].w < surface) cubeIndex |= 128;

    // Create triangles for current cube configuration
    for (int i = 0; triangleTable[cubeIndex][i] != -1; i +=3) {
        // Get indices of corner points A and B for each of the three edges
        // of the cube that need to be joined to form the triangle.
        int a0 = edgeConnections[triangleTable[cubeIndex][i]].x;
        int b0 = edgeConnections[triangleTable[cubeIndex][i]].y;

        int a1 = edgeConnections[triangleTable[cubeIndex][i+1]].x;
        int b1 = edgeConnections[triangleTable[cubeIndex][i+1]].y;

        int a2 = edgeConnections[triangleTable[cubeIndex][i+2]].x;
        int b2 = edgeConnections[triangleTable[cubeIndex][i+2]].y;

        Triangle tri;
        tri.vertexA = interpolateVerts(cubeCorners[a0], cubeCorners[b0]);
        tri.vertexB = interpolateVerts(cubeCorners[a1], cubeCorners[b1]);
        tri.vertexC = interpolateVerts(cubeCorners[a2], cubeCorners[b2]);
        triangles.Append(tri);
        if(i >= 14) break;
    }


}
